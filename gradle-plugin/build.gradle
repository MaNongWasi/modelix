plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

sourceCompatibility = 11

repositories {
    mavenCentral()
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
}

dependencies {
    compileOnly fileTree(dir: '../artifacts/mps/lib', include: '*.jar')
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava.dependsOn(":mps:resolveMps")

jar {
    manifest {
        attributes("Implementation-Version": modelixVersion)
        attributes("modelix-Version": modelixVersion)
        attributes("MPS-MajorVersion": mpsMajorVersion)
        attributes("MPS-MinorVersion": mpsMinorVersion)
        attributes("MPS-Version": mpsVersion)
    }
}

publishing {
    publications {
        modelixGradlePlugin(MavenPublication) {
            groupId project.group
            version project.version

            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'modelixorg'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/modelix/modelix'
        version {
            name = 'v' + project.version
            desc = 'Version ' + project.version
            released = new Date()
            vcsTag = 'v'+project.version
        }
    }

    publications = ['modelixGradlePlugin']
    dryRun = false
    publish = true
    override = true
}
