plugins {
    id "java"
    id "application"
    id "com.diffplug.gradle.spotless"
    id 'maven-publish'
    id "com.jfrog.bintray"
}

group = 'org.modelix'
description = 'Model Server offering access to model storage'
version = '1.0-SNAPSHOT'

defaultTasks 'build'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'org.json', name: 'json', version:'20180813'
    compile group: 'org.java-websocket', name: 'Java-WebSocket', version:'1.4.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version:'4.4'
    compile group: 'io.lettuce', name: 'lettuce-core', version:'5.1.8.RELEASE'
    compile group: 'log4j', name: 'log4j', version:'1.2.17'
    compile group: 'org.apache.ignite', name: 'ignite-core', version:'2.7.5'
    compile group: 'org.apache.ignite', name: 'ignite-spring', version:'2.7.5'
    compile group: 'org.apache.ignite', name: 'ignite-indexing', version:'2.7.5'
    compile group: 'org.postgresql', name: 'postgresql', version:'42.2.14'
    
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.21.v20190926'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-servlet', version: '9.4.21.v20190926'
	compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.21.v20190926'
    compile group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: '9.4.21.v20190926'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '9.4.21.v20190926'

    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    compile group: 'com.beust', name: 'jcommander', version: '1.7'
}

task copyLibs(type: Copy) {
    into "$buildDir/libs"
    from configurations.default
}

assemble.finalizedBy(copyLibs)

application {
    mainClassName = "org.modelix.model.server.Main"
}

publishing {
    publications {
        modelServer(MavenPublication) {
            groupId project.group
            version project.version

            from components.java
        }
    }
}

bintray {
    user = rootProject.findProperty('bintray_user')
    key = rootProject.findProperty('bintray_api_key')
    pkg {
        repo = 'modelix-oss-maven'
        name = project.name
        userOrg = 'modelix'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/modelix/modelix.git'
        version {
            name = 'v' + project.version
            desc = 'Version ' + project.version
            released = new Date()
            vcsTag = 'v'+project.version
        }
    }

    publications = ['modelServer']
    dryRun = false
    publish = true
    override = true
}

spotless {
    java {
        googleJavaFormat(sourceCompatibility.toString()).aosp()
        licenseHeader '/*\n' +
                ' * Licensed under the Apache License, Version 2.0 (the "License");\n' +
                ' * you may not use this file except in compliance with the License.\n' +
                ' * You may obtain a copy of the License at\n' +
                ' *\n' +
                ' *  http://www.apache.org/licenses/LICENSE-2.0\n' +
                ' *\n' +
                ' * Unless required by applicable law or agreed to in writing,\n' +
                ' * software distributed under the License is distributed on an\n' +
                ' * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY\n' +
                ' * KIND, either express or implied.  See the License for the\n' +
                ' * specific language governing permissions and limitations\n' +
                ' * under the License. \n' +
                ' */\n' +
                '\n'
    }
}